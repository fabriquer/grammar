:source-highlighter: pygments
:pygments-style: tango
:toc: right
:toclevels: 3

= The Fabrique build language

== Motivation

== Grammatical elements

=== Comments and whitespace

Comments and whitespace look much like they do in shell scripts.

.Examples
[source, fabrique]
----
# Fabrique uses single-line comments that start with a '#' character

# Here's another comment:
sources = files(a.c b.c);     # our source files

#
# Begin/end multiline comments are not supported: just use multiple single-line
# comments together in a row.
#
----

Single-line comments start with a `#` character.
There are no multi-line comments.
Comments are currently ignored completely.
We may add semantics to comments in the future (a la Doxygen), but we
haven't yet.

Whitespace in Fabrique is not significant (like, e.g., Python):
spaces, tabs and newlines are all ignored.

.Grammar
[source, antlr]
----
Comment		: '#' ~[\r\n]* -> skip;
WS		: [ \t\r\n]+ -> skip;
----

=== Values

A Fabrique file is made up of [optionally-]named values.

.Example
[source, fabrique]
----
source_files = files(a.cc b.cc);
count:int = source_files.length();

object_files = compile(source_files);
executable = link(object_files);
----

.Grammar
[source, antlr]
----
file		: (values+=value)* EOF;
value		: (name=Identifier (':' type)* '=')? expression ';';
----

.Semantics
Most values bind a name (which must be a <<Identifiers,legal identifier>>)
to the result of evaluating an <<Expressions,expression>>.
It's also possible to simply use an expression without naming it, but that's
only really useful for expressions that cause side effects (e.g., declaring
a build step without giving it an explicit name).
Values are like variables in many languages, but with a few key aspects
from functional programming:

1. values are immutable, so there is no distinction between declaration and
   assignment;
2. a value's type may be specified explicitly or _inferred_ by Fabrique:
   a. all expressions (on the RHS) evaluate to a value with a type, so that
      type can be found when the build is configured;
   b. if an explicit type is given, Fabrique will check that the expression
      evaluates to a <<Subtypes,subtype of the explicit type>>;
3. value names may not be redefined within a scope;

Otherwise, values share some characteristics with statements in other
curly-brace languages like C and Java:

1. values are terminated by a semicolon;
2. if the value has a name, it must be followed by an equals sign.


=== Identifiers

Identifiers looks like those in many programming languages.

.Examples
[source, fabrique]
----
# This is a sensible name:
a_valid_identifier;

# This is a less-sensible but still-legal name:
another_identifier_that_is_valid_even_if_it_is_2_long_4_G00D_STYLE;

# This is not an identifier:
1badname;
----

Fabrique identifiers currently only use characters from the ASCII character
set.
Careful thought will be required in the future about adding support for other
characters.
Identifiers must start with a letter and then may continue with letters,
numbers and underscores.
It is conventional (but not mandated) to use underscore-separated
lower-case names.

.Grammar
[source, antlr]
----
Identifier	: [a-zA-Z] [a-zA-Z0-9_]*;
----


=== Expressions

Almost everything in Fabrique is an expression that can be evaluated.


==== Build actions

At the heart of a Fabrique build description is a set of _build actions_.
Each build action represents a step that transforms
input <<Files,file(s)>> to output <<Files,file(s)>>.
Common build actions include compiling source files, linking libraries and
executables and generating code (e.g., with `rpcgen` and `yacc`).
Actions defined in Fabrique source files execute shell commands with string
substitution (e.g., `'${cc} ${cflags} -c ${src} -o ${obj}'`); these commands
are executed at build time (rather than compile time).

.Examples
[source, fabrique]
----

cc = action('${cc} ${flags} -c ${src} -o ${obj}', description = 'Compiling ${src}'
	<= src: file[in], obj: file[out], flags: list[string] = []);

obj:file = cc(src_file);
----

An action contains:

- a command to run,
- [optional] arguments to the action itself (e.g., `description`) and
- parameters that invocations of the action need to provide.

Actions have a <<Function types,function type>>,
e.g., `(file[in])=>file[out]`, and can be <<Calls,called>> like functions.

The command to execute can include `${varname}`-style substitution patterns.
These substitutions can capture values from the parameters required by the
action, or else from the arguments provided to the action, or if not found
there, captured from the script that the action is defined in
(useful for selecting tools, e.g., `${cc}`).

Arguments to the action (after the command) must be keyword arguments
(see <<Calls>> for more details).
A `description` argument can be used to customize build log output
(e.g., `'Compiling ${src}'`).
Other arguments are made available for substitution into the command.

Action parameters can be of any defined <<Types,type>>.
Parameters of `file` type should specify whether the file in question is an
input to the rule (`file[in]`) or an output (`file[out]`).
This is critical for calculating the graph of build dependencies;
it is an error to omit the `in` or `out` tag.

.Grammar
[source, antlr]
----
buildAction
	: 'action' '(' command=expression
		(',' keywordArguments)?
		('<-' parameters)?
		')'
	;
----


==== Calls

Values of function type (including both <<Build actions,build actions>>
and <<Functions,functions>>) can be _called_ using a syntax that looks like
a lot of languages: with comma-separated arguments inside of parentheses.

.Examples
[source, fabrique]
----
noArguments();
positionalArguments(1, 2, 'hello', true);
keywordArguments(what='hello', who = 'world');
mixedArguments(1, 2, x=3);
----

Arguments may be _positional_ (parameter name unspecified) or
_keyword_ (parameter name specified).
Positional arguments, if present, must come first â€” otherwise it would be
difficult to tell which parameter you intend each argument to be passed to.
It is permissible to use an extra comma at the end of the argument list;
this can help reduce the sizes of diffs when parameters and arguments are
added or removed.

.Grammar
[source, antlr]
----
// Anything with a function type can be called
call	: term '(' arguments? ')' ;

arguments
	: positionalArguments ',' keywordArguments
	| positionalArguments
	| keywordArguments
	;

keywordArgument	: Identifier '=' expression ;
keywordArguments: (args+=keywordArgument (',' args+=keywordArgument)* ','?) ;

positionalArguments	: expression (',' expression)* ','? ;
----


==== Compound expressions

A compound expression is a braced list of values followed by an expression.
The values describe intermediate state that can be used by the final
"result" expression.

.Example
[source, fabrique]
----
{
	src = file(basename + '.c');
	obj = compile(src);
	binary = link(obj);

	binary
}
----

Compound expressions can help clarify the meaning of expressions like
<<Conditionals,conditionals>> by adding braces.
They also constrain the scope of intermediate variables, which can also help
with clarity of expression.

.Grammar
[source, antlr]
----
// Zero or more value definitions and a result
compoundExpr	: '{' (values+=value)* result=expression '}' ;
----


==== Conditionals

Rather than an `if` statement, Fabrique has a conditional `if`
_expression_.
This functions like the ternary operator in C, but is hopefully a bit more
readable:

[source, fabrique]
----
like_an_if_statement = if (y > 0) { y } else { -y };
----

Unlike other popular languages, the parentheses around the condition
(making it a <<Parenthetical expressions,parenthetical expression>>)
and the braces around the "then" and "else" clauses
(making them <<Compound expressions,compound expressions>>)
are entirely optional:

[source, fabrique]
----
more_succinct = if y > 0 y else -y;
----

Whether or not to use the extra punctuation is a question of clarity and style.

.Grammar
[source, antlr]
----
conditional
	: 'if'
	  condition=expression
	  thenClause=expression
	  'else'
	  elseClause=expression
	;
----


==== Field references

<<Records>> contain fields, which are accessed using the dot (`.`) operator.

.Examples
[source, fabrique]
----
# Access a single field:
simple = foo.bar;

# Access a field within a field (within a field, within a field, ...):
nested = foo.bar.baz;

# Slightly more complicated things:
complex = (if something foo else bar).baz.wibble;

veryComplex = {
	x = record { foo = 42 };
	y = record { bar = x };

	y
}.bar.foo;
----


The base of a field reference (the record containing the fields) must be a
<<Terms,term>>, but that includes compound expressions and
parenthetical expressions.
Using one of those constructs, you can construct expressions that are quite
sophisticated.
However, good style would suggest that you keep visual complexity under control.

.Grammar
[source, antlr]
----
// Access a field within a record
fieldReference
	: term '.' Identifier
	| fieldReference '.' Identifier     // explicitly left-recursive
	;
----


==== Files

Files are the primary artifacts of almost any build.
Fabrique provides two ways to describe files: individually using the `file`
builtin or collectively using the `files` construct.


===== File declarations

Individual files can be described with the `file` builtin.

.Examples
[source, fabrique]
----
file('foo.c', cflags = [ '-D' 'FOO' ]);
----

This builtin looks like a regular function, but its name is a reserved word
in the language and it requires that the first (positional) argument be a
string containing a filename (usually relative to the directory containing the
current Fabrique file).
Additional arguments are stored as properties of the file; these properties
can be accessed by <<Build actions,build actions>> and <<Functions,functions>>
that accept file arguments.

.Grammar
Grammatically, a file declaration looks like <<Calls,any other call>>.


===== File lists

When describing lots of files, it can be inconvenient to wrap each of them in
a `file` declaration.
Fabrique also provides a dedicated syntax for describing lots of files:
the _file list_.
A file list is wrapped by `files()` like a call, but internally its syntax
permits the use of raw filenames.

.Examples
[source, fabrique]
----
files(
	foo.c
	bar.c
	,
	cflags = ['-Wall'], arg2 = 42
)
----

A file list contains a sequence of raw filenames, optionally followed by some
keyword arguments (see <<Calls>> for details).
Just like file declarations, the keyword arguments are turned into fields
attached to the file list (and visible as fields each file in the list when
the list is iterated over with a <<Foreach,foreach expression>>).
Arguments to embedded file declarations can override arguments passed to the
file list (e.g., `cflags` in the example above).

.Grammar
[source, antlr]
----
// Some number of files with common properties
fileList
	: FileListStart FilenameLiteral*
	  (FileListEnd | (FileListArgs keywordArguments ')'))
	;

// With lexer rules:
FileListStart	: 'files(' -> pushMode(FileList) ;   // enter "file list" mode
FileListEnd	: ')' -> popMode ;
FileListArgs	: ',' -> popMode ;
FilenameLiteral	: [A-Za-z0-9_./]+ ;
FileListWS	: [ \t\r\n]+ -> skip ;
----


==== Foreach

A foreach loop transforms one sequence into another.

.Examples
[source, fabrique]
----
# A somewhat verbose way to use foreach, a bit like a conventional for loop:
y = foreach x <- [ 1 2 3 ]
{
	x + 1
};

# Equivalently, a shorter way of expressing the same thing:
y = foreach x <- [ 1 2 3 ] x + 1;
----

Unlike most general-purpose computation languages, Fabrique doesn't have
statements, so it doesn't have a conventional looping primitive.
What it does have is the ability to transform one sequence of things
(e.g., source files) into a sequence of related things (e.g., object files).
This is much like applying a mapping operation to an iterator, but it's spelled
`foreach` to be a bit more accessible.

The `foreach` expression includes:

 * the keyword `foreach`,
 * the name of the loop variable,
 * the sequence to map from and
 * an expression representing the transformation of one sequence element.

The entire `foreach` expression evaluates to a sequence of new elements.

.Grammar
[source, antlr]
----
// Transforms one sequence into another
foreach
	: 'foreach' Identifier (':' type)? '<-' src=expression body=expression
	;
----


==== Functions

As a somewhat functional language, functions are pretty important to Fabrique!
They are, together with <<Records,records>>, the primary means of building up
higher-level abstractions (e.g., toolchains) from lower-level abstractions
(e.g., <<Build actions,build action>> invocation).

.Examples
[source, fabrique]
----
# These default flags will be visible in the `compile` function below
default_flags = record
{
	object = [ '-fPIC' ];
};

compile = function(srcs:list[file], common_flags:list[string] = [],
                   deps:list[file[in]] = []): list[file]
{
	foreach src <- srcs
	{
		obj = src + '.o';
		flags = (src.cxxflags ? common_flags) + default_flags.object;

		compile_one(src, obj, flags, deps);
		obj
	}
};
----

As in other languages, functions take parameters and return a value.
Parameters can have default arguments in case no arguments are passed to them
in the function <<Calls,call>>.

Fabrique functions are closures that capture values from the surrounding scope.
This can be useful for providing <<Modules,module>>-level defaults.

.Grammar
[source, antlr]
----
// Transforms parameters into a result
function
	: 'function' '(' parameters ')' (',' type)?
	  body=expression
	;

parameters	: (parameter (',' parameter)*)? ;
parameter	: Identifier ',' type ('=' expression)? ;
----


==== Literals

Fabrique (currently) supports three literal types: boolean values,
integers and strings (single- or double-quoted).

.Examples
[source, fabrique]
----
p:bool = true;
q = false;
integer:int = 42;
single_quoted = 'double quotes (") are OK';
double_quoted = "single quotes (') are OK";
list_of_numbers = [ 1 2 3 ];
list_of_bools = [ true false ];
----

Boolean literals are spelled in all lower-case (`true` and `false`).
Integers are currently specified in a base-10 representation; future versions
of this grammar may also support binary, octal or hex literals.
Strings may be single- or double-quoted; there is not semantic distinction
between them, the two types exist purely for convenience of writing strings
that contain quote characters.
**We do not yet support escape characters.**

.Grammar
[source, antlr]
----
// We support boolean, numeric and string literals
literal
	: BoolLiteral
	| IntLiteral
	| StringLiteral
	;

BoolLiteral	: 'true' | 'false' ;
IntLiteral	: [0-9]+ ;
StringLiteral	: ('\'' .*? '\'') | ('"' .*? '"');
----


==== Lists

Lists are containers for like values and do not use comma separators.

.Examples
[source, fabrique]
----
x:int = 42;
y:special_int = some_special_kind_of_int();

l:list[int] = [ 1 2 3 x y ];   # the type of this is list[int]
----

The type of the list is taken to be "list of the supertype of all of the
list's elements".

.Grammar
[source, antlr]
----
// Zero or more expressions of (some) like type
list		: BracketOpen (values+=expression)* BracketClose ;
----


==== Parenthetical expressions

An expression enclosed in parentheses is treated as a <<Terms,_term_>>
and can therefore appear in places that a more general expression expression
cannot.
It is also evaluated before subexpressions that are not inside of the
parentheses.

.Examples
[source, fabrique]
----
# Specifying an order of operations:
x = p and (q or r);

# Choose which function we want to call:
y = (if useF f else g)(x);

# Without parentheses, g(x) is evaluated before the if condition:
bad = if useF f else g(x);
----

Just like every other language, Fabrique has an order of operations for its
operators, and parentheses can be used to override that order.
In the example above, the expression for `x` will only be true if `p` is true;
without parentheses, `r` alone would be sufficient to make `x` true.

Parentheses are also helpful when we want to use an expression inside of an
expression that only works with terms.
For example, a <<Calls,call>> or <<Field references,field reference>>
requires its target (the function to call or the record to look in) to be
a term.
The example code above shows how we can use an expression like a
<<Conditionals,conditional>> to choose which function we want to call,
then use that conditional within parentheses to actually specifify the
call target.


.Grammar
[source, antlr]
----
parentheticalExpression	: '(' expression ')' ;
----


==== Type declarations

Types can be declared using the keyword `type` followed by a normal
<<Types,type annotation>>.

.Examples
[source, fabrique]
----
# An empty record type:
foo:type = type record[];

# A more interesting record type:
bar = type record[x:int, y:string, z:record[wibble:int]];

# A parameterized type:
baz = type list[string];

# A function type:
wibble:type = type (baz)->int;

f:wibble = function (names:baz) { names.size };
----

Type declarations are like typedefs in other languages: they allow a new name
to be used for another (possibly complicated) type.
There is currently no support for parameterizing declared types
(e.g., `foo[int]`).

.Grammar
[source, antlr]
----
typeDeclaration	: 'type' type ;
----


=== TODO

==== Binary operators

////
binaryOperation	: logicalOperation | comparison | sum ;

logicalOperation: lhs=comparison LogicOp rhs=logicalOperation ;
LogicOp		: 'and' | 'or' | 'xor' ;

logicOrCompare	: logicalOperation | comparison ;

comparison	: lhs=sumOrTerm CompareOp rhs=sumOrTerm ;
CompareOp	: '<' | '<=' | '>' | '>=' | '==' | '!=' ;

sumOrTerm	: sum | term ;

sum		: lhs=term ('+' | '::' | '.+') rhs=sumOrTerm ;
////


==== Field queries

////
/**
 * A field query lets us provide a default value for a record field that may
 * or may not exist.
 *
 * ```fab
 * debug = args.debug ? false;
 * ```
 */
fieldQuery	: base=term '.' field=Identifier '?' defaultValue=expression ;
////


==== Records

==== Terms

==== Types

===== Simple types

===== Parametric types

===== Record types

===== Function types

===== Subtypes

////
/**
 * There are four syntaxes for naming types:
 *
 *  - function types: `(type1, type2) => resultType`
 *  - record types: `record[field1:type2, field2:type2]`
 *  - parametric types: `simpleName[typeArg1, typeArg2]`
 *  - simple types: `int`, `string`, `foo`, etc.
 */
type
	: functionType
	| recordType
	| parametricType
	| simpleType
	;

functionType	: '(' type_list ')' '=>' type ;
recordType	: 'record' '[' (fieldType (',' fieldType)*)? ','? ']' ;
fieldType	: Identifier ':' type ;
parametricType	: simpleType '[' type_list ']' ;
simpleType	: Identifier ;

type_list : type (',' type)* ','? ;

////


==== Unary operations

////
unaryOperation	: ('not' | '-' | '+') expression ;
////

